plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'java'
	id 'jacoco'
	id 'net.saliman.cobertura' version '2.6.0'
	id 'com.github.kt3k.coveralls' version '2.8.2'
	id "org.sonarqube" version "2.7"
	id 'com.sourcemuse.mongo' version '1.0.7'
}

apply from: "$rootDir/itest.gradle"
apply from: "$rootDir/e2etest.gradle"
apply plugin: 'io.spring.dependency-management'

jacocoTestReport {
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
		fileTree(dir: it, 
				exclude:	['org/unifi/ft/rehearsal/RehearsalRoomGradleApplication.java',
							'org/unifi/ft/rehearsal/model/**',
							'org/unifi/ft/rehearsal/configurations/**',
							'org/unifi/ft/rehearsal/exceptions/**',
							'org/unifi/ft/rehearsal/model/**'])
		})
	}
}



jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.5
			}
		}

		rule {
			enabled = false
			element = 'CLASS'
			includes = ['org.unifi.ft.rehearsal.services.*']

			limit {
				counter = 'LINE'
				value = 'TOTALCOUNT'
				maximum = 0.3
			}
		}
	}
}



cobertura.coverageFormats = ['html', 'xml']


sonarqube {
	properties {
		property 'sonar.exclusions', "**/org/unifi/ft/rehearsal/RehearsalRoomGradleApplication.java, **/org/unifi/ft/rehearsal/model/**, **/org/unifi/ft/rehearsal/configurations/**, **/org/unifi/ft/rehearsal/exceptions/**"
	}
}


group = 'org.unifi.ft'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	gradlePluginPortal()
}

test {
	testLogging {
		events "failed", "standardOut", "standardError"
	}
	test.outputs.upToDateWhen {false}
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'joda-time:joda-time:2.10.1'
	implementation group: 'org.webjars', name: 'bootstrap', version: '3.3.7-1'
	implementation group: 'org.webjars', name: 'webjars-locator', version: '0.36'

	testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.41.0'
	testImplementation("io.github.bonigarcia:webdrivermanager:3.4.0")
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'info.cukes:cucumber-java8:1.2.4'
	testImplementation group: 'info.cukes', name: 'cucumber-spring', version: '1.2.4'
	testImplementation 'info.cukes:cucumber-junit:1.2.4'
}


